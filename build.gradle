plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

defaultTasks 'build'

group 'eu.decentsoftware.holograms'
version '2.7.6'
description 'Lightweight yet very powerful hologram plugin with many features and configuration options.'

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://repo.codemc.io/repository/nms/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }
    maven { url = 'https://repo.clojars.org/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot:1.13-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.1'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.8.0'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation "com.github.puregero:multilib:1.1.8"

    // Annotations
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    annotationProcessor 'org.jetbrains:annotations:23.0.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
            artifactId 'decentholograms'
        }
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate "org.bstats", "eu.decentsoftware.holograms.metrics"
    relocate "com.github.puregero.multilib", "eu.decentsoftware.holograms.multilib"
}